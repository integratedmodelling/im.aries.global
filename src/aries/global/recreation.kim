namespace aries.global.recreation
	using (HEMEROBY_TABLE, RECREATION_OPPORTUNITY_TABLE) from aries.global.recreation.tables;

model im:Impedance of behavior:TravelConnection
	observing 
	// switch to this when the resolver has been smartened
//		distance to (infrastructure:City or infrastructure:Town or infrastructure:Village) in m
		distance to infrastructure:City in m 
			named distance_from_cities,
		im:Duration of behavior:TravelConnection in day named travel_time
	set to [
		travel_time < 0.02085 
			? (451/(450 + Math.exp(1.12 * (10**-4) * distance_from_cities)))  
		    : (451/(450 + Math.exp(1.12 * (10**-4) * (distance_from_cities + 30000))))
	];

@documented(recreation.values.actual)
model im:Realized value of behavior:Outdoors behavior:Recreation 1 to 9
	observing
		im:Normalized (im:Duration of behavior:TravelConnection) named travel_time,
		im:Normalized (im:Potential value of behavior:Outdoors behavior:Recreation) named potential_recreation_supply
	lookup (travel_time, potential_recreation_supply, ?) into RECREATION_OPPORTUNITY_TABLE;

@documented(recreation.values.potential)
model im:Potential value of behavior:Outdoors behavior:Recreation
	observing 
		magnitude of proportion of behavior:Outdoors in behavior:Recreation named human_influence,
		distance to conservation:ProtectedArea in m named distance_to_pristine_areas,
		distance to earth:Coastline in m    named distance_to_coast,
		distance to earth:Stream in m       named distance_to_streams,
		distance to earth:WaterBody in m    named distance_to_lakes optional,
		distance to earth:MountainPeak in m named distance_to_mountains
	set to [
		human_influence * 
		      ((nodata(distance_to_pristine_areas) ? 0 : distance_to_pristine_areas) 
			  + (nodata(distance_to_lakes) ? 0 : distance_to_lakes) 
			  + (nodata(distance_to_streams) ? 0 : distance_to_streams) 
			  + (nodata(distance_to_mountains) ? 0 : distance_to_mountains) 
			  + (nodata(distance_to_coast) ? 0 : distance_to_coast))
	] then [ self.invert() ];

@documented(recreation.demand)
model ses:Demanded value of behavior:Outdoors behavior:Recreation 0 to 2
	observing
		im:Impedance of behavior:TravelConnection         named impedance_for_travel,
		im:Normalized count of demography:HumanIndividual named population_density_factor
	set to [impedance_for_travel + population_density_factor/2];

@todo('makes no sense unless aggregated between areas of greater demand and offer. Demand 
      for outdoors recreation is not local by definition.')
@todo('appropriate colormap (linked to im:Net)')
@documented(recreation.balance)
model im:Net value of behavior:Outdoors behavior:Recreation -1 to 1
	observing
		im:Normalized (im:Potential value of behavior:Outdoors behavior:Recreation) named potential_recreation_supply,
		im:Normalized (ses:Demanded value of behavior:Outdoors behavior:Recreation) named demand_for_recreation
	set to [(potential_recreation_supply ** 0.5) * (demand_for_recreation ** 0.5)];

@documented(recreation.hemeroby)
@todo('annotation is all wrong')
private model magnitude of proportion of behavior:Outdoors in behavior:Recreation
	observing landcover:LandCoverType named land_cover_type
	lookup (land_cover_type, ?) into HEMEROBY_TABLE;
