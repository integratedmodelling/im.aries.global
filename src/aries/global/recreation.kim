/**
 * This file is part of im.aries.global.
 * <license>
 */
namespace aries.global.recreation
	using (HEMEROBY_TABLE, RECREATION_OPPORTUNITY_TABLE) from aries.global.recreation.tables;

@todo('need a better concept for whatever this is the value of, and an alias for it')
@documented(recreation.values.actual)
model value of behavior:Recreational behavior:Activity 1 to 9
	observing
		im:Normalized (im:Duration of behavior:TravelConnection) named traveltime,
		im:Normalized (im:Potential value of behavior:Recreational behavior:Activity) named potential
	lookup (traveltime, potential, ?) into RECREATION_OPPORTUNITY_TABLE
;

@documented(recreation.impedance)
model im:Impedance of behavior:TravelConnection
	observing 
		distance to infrastructure:City in m named citydistance,
		im:Duration of behavior:TravelConnection in day named traveltime
	on definition set to [
		traveltime < 0.02085 
			? (451/(450 + Math.exp(1.12 * (10**-4)*citydistance)))  
		    : (451/(450 + Math.exp(1.12 * (10**-4)*(citydistance + 30000))))
	]
;

@todo("use meaningful names; use a clamping function for the nodata instead of the conditional")
@todo("use sensible decay functions for all distances")
@todo("for later: use distance along various networks (road, air, walk) to supplement 
	   the others for different classes of users")
@documented(recreation.values.potential)
model im:Potential value of behavior:Recreational behavior:Activity 
	observing 
		magnitude of proportion of behavior:Recreational behavior:Activity named hi,
		distance to conservation:ProtectedArea in m named pa,
		distance to earth:CoastLine in m named wa,
		distance to earth:Stream in m named st,
		distance to earth:Lake in m named lk,
		distance to earth:MountainPeak in m named mp
	on definition set to [
		hi * 
			((nodata(pa) ? 0 : pa) + 
				(nodata(wa) ? 0 : wa) + 
				(nodata(st) ? 0 : st) + 
				(nodata(lk) ? 0 : lk) + 
				(nodata(mp) ? 0 : mp))
	]
;

@documented(recreation.demand)
model ses:Demand value of behavior:Recreational behavior:Activity 0 to 2
	observing
		im:Impedance of behavior:TravelConnection named impedance,
		im:Normalized count of demography:HumanIndividual named population
	on definition set to [ impedance + population/2 ]
;

@main
@todo("differential isn't really balance. Need a way to annotate that.")
@documented(recreation.balance)
model im:Differential (value of behavior:Recreational behavior:Activity) -1 to 1
	observing
		im:Normalized (im:Potential value of behavior:Recreational behavior:Activity) named potential,
		im:Normalized (ses:Demand value of behavior:Recreational behavior:Activity) named demand
	on definition set to [ (potential ** .5) * (demand ** .5) ]
;

@documented(recreation.hemeroby)
private model magnitude of proportion of behavior:Recreational behavior:Activity
	observing landcover:LandCoverType named landcover
	lookup (landcover, ?) into HEMEROBY_TABLE
;
