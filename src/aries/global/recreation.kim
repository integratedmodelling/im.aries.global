namespace aries.global.recreation
	using (HEMEROBY_TABLE, RECREATION_OPPORTUNITY_TABLE) from aries.global.recreation.tables;

@documented(recreation.impedance)
model im:Impedance of behavior:TravelConnection
	observing 
		distance from infrastructure:HumanSettlement in m 
			named distance_from_human_settlements,
		im:Duration of behavior:TravelConnection in day named travel_time
	set to [
		travel_time < 0.02085 
			? (451/(450 + Math.exp(1.12 * (10**-4) * distance_from_human_settlements)))  
		    : (451/(450 + Math.exp(1.12 * (10**-4) * (distance_from_human_settlements + 30000))))
	];

@documented(recreation.values.potential)
model im:Potential value of behavior:Outdoor behavior:Recreation 1 to 9
	observing
		im:Normalized (im:Duration of behavior:TravelConnection) named travel_time,
		im:Normalized (im:Theoretical value of behavior:Outdoor behavior:Recreation) named theoretical_recreation_supply
	lookup (travel_time, theoretical_recreation_supply, ?) into RECREATION_OPPORTUNITY_TABLE;

@documented(recreation.values.theoretical)
model im:Theoretical value of behavior:Outdoor behavior:Recreation
	observing 
		proportion of (not conservation:Pristine) earth:Region caused by demography:HumanPopulation named human_influence,
		distance to conservation:ProtectedArea in m named distance_to_pristine_areas,
		distance to earth:Coastline in m    named distance_to_coast,
		distance to earth:Waterway in m     named distance_to_streams,
		distance to earth:WaterBody in m    named distance_to_lakes,
		distance to earth:MountainPeak in m named distance_to_mountains
	set to [
		human_influence * 
		      ((nodata(distance_to_pristine_areas) ? 0 : distance_to_pristine_areas) 
			  + (nodata(distance_to_lakes) ? 0 : distance_to_lakes) 
			  + (nodata(distance_to_streams) ? 0 : distance_to_streams) 
			  + (nodata(distance_to_mountains) ? 0 : distance_to_mountains) 
			  + (nodata(distance_to_coast) ? 0 : distance_to_coast))
	] then [ self.invert() ];

@main
@documented(recreation.demand)
model ses:Demanded value of behavior:Outdoor behavior:Recreation 0 to 2
	observing
		im:Impedance of behavior:TravelConnection         named impedance_for_travel,
		im:Normalized count of demography:HumanIndividual named population_density_factor
	set to [impedance_for_travel + population_density_factor/2];

@documented('recreation.value')
model value of behavior:Outdoor behavior:Recreation 0 to 1
	observing
		im:Normalized (im:Potential value of behavior:Outdoor behavior:Recreation) named potential_recreation_supply,
		im:Normalized (ses:Demanded value of behavior:Outdoor behavior:Recreation) named demand_for_recreation
	set to [(potential_recreation_supply ** 0.5) * (demand_for_recreation ** 0.5)];

@documented(recreation.balance)
model im:Net value of behavior:Outdoor behavior:Recreation
	observing
		im:Normalized (im:Potential value of behavior:Outdoor behavior:Recreation) named potential_recreation_supply,
		im:Normalized (ses:Demanded value of behavior:Outdoor behavior:Recreation) named demand_for_recreation
	set to [potential_recreation_supply - demand_for_recreation];

@documented(recreation.hemeroby)
model proportion of (not conservation:Pristine) earth:Region caused by demography:HumanPopulation
	observing landcover:LandCoverType named land_cover_type
	lookup (land_cover_type, ?) into HEMEROBY_TABLE
	set to [self/7.0];
