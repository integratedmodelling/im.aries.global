@documented(pollination)
namespace aries.global.pollination
	using (FLOWERING_PROBABILITY_TABLE, NESTING_PROBABILITY_TABLE) 
		from aries.global.pollination.tables;

@documented(pollination.nesting-suitability)
model occurrence of agriculture:Pollinator ecology:Nesting
	observing landcover:LandCoverType named landcover
	lookup (landcover) into NESTING_PROBABILITY_TABLE;

@documented(pollination.flower-availability)
model probability of ecology:Flowering
	observing landcover:LandCoverType named landcover
	lookup (landcover) into FLOWERING_PROBABILITY_TABLE;

@documented(pollination.insectoccurrence.landscape)	
model occurrence of agriculture:Pollinator biology:Insect caused by ecology:Landscape
	observing
		distance to earth:Waterway in m  named distance_to_streams,
		distance to earth:WaterBody in m named distance_to_large_lakes,
		probability of ecology:Flowering named flowering_suitability,
		occurrence of agriculture:Pollinator ecology:Nesting named nesting_suitability
	set to [
		def lake_effect   = nodata(distance_to_large_lakes) ? 0 : Math.exp(-distance_to_large_lakes/0.5)
		def stream_effect = nodata(distance_to_streams)     ? 0 : Math.exp(-distance_to_streams/0.5)
		return stream_effect + lake_effect + (nesting_suitability * flowering_suitability)
	];

@documented(pollination.insectoccurrence.weather)	
model occurrence of agriculture:Pollinator biology:Insect caused by earth:Weather
	observing
		im:Mean earth:AtmosphericTemperature during im:Year in Celsius named air_temperature,
		im:Mean earth:SolarRadiation during im:Year in J named solar_radiation
	set to [0.62 + 1.027 * air_temperature + 0.006 * solar_radiation];
	
@main
@documented(pollination.insectoccurrence)	
model occurrence of agriculture:Pollinator biology:Insect
	observing 
		occurrence of agriculture:Pollinator biology:Insect caused by earth:Weather 
			named insect_activity,
		occurrence of agriculture:Pollinator biology:Insect caused by ecology:Landscape
			named landscape_suitability
	set to [insect_activity * (landscape_suitability/insect_activity.max)];

@main		
@documented(pollination.netbenefit)	
model im:Net value of ecology:Pollination
	observing
		im:Normalized ratio of agriculture:Pollinated agriculture:Yield to agriculture:Yield
			named pollinated_yield,
		occurrence of agriculture:Pollinator biology:Insect 
			named pollinator_occurrence
	set to [pollinator_occurrence - pollinated_yield];
